plugins {
    id("fabric-loom") version("${loom_version}")
}

dependencies {
    minecraft(libs.minecraft)
    mappings(loom.officialMojangMappings())

    modImplementation(libs.bundles.fabric)
    modImplementation(libs.clothConfigFabric)

    implementation(project(":common"))
}

tasks {
    processResources {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE

        from("src/main/resources", "../common/src/main/resources")

        filesMatching("fabric.mod.json") {
            expand(project.properties)
        }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.release.set(17)
    }
}

loom {
    mixin {
        getDefaultRefmapName().set("fabric.${modId}.refmap.json")
    }

    runs {
        create("skipSignClient") {
            client()

            property("fabric.log.level", "debug")
            vmArg("-XX:+ShowCodeDetailsInExceptionMessages")
        }
        create("skipSignServer") {
            server()

            property("fabric.log.level", "debug")
            vmArg("-XX:+ShowCodeDetailsInExceptionMessages")
        }
    }
}
